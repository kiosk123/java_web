apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'war'
apply plugin: 'idea'

group = 'org.springframework.samples.service.service'
version = '0.0.1-SNAPSHOT'

description = """"""

sourceCompatibility = 1.7
targetCompatibility = 1.7

def queryDslOutput = file("src/main/querydsl_generated")


tasks.withType(JavaCompile) {
	options.encoding = 'EUC-KR'
}

eclipse{ 
        wtp{ 
            facet{
            facet name:'jst.web',version:'3.0' 
            facet name:'jst.java',version:'1.7'
        }
    } 
}

repositories {       
     mavenCentral()
}

//java비즈니스 소스와 queryDSL이 생산해낸 소스를 분리하여 모두 소스 디렉토리로 지정
sourceSets {
    main {
        java {
            srcDirs 'src/main/java', 'src/main/querydsl_generated'
        }
    }
}

task generateQueryDSL(type: JavaCompile, group: 'build') {
    doFirst {
        if (!queryDslOutput.exists()) {
            logger.info("Creating `$queryDslOutput` directory")
 
            if (!queryDslOutput.mkdirs()) {
                throw new InvalidUserDataException("Unable to create `$queryDslOutput` directory")
            }
        }
    }
 
    source = sourceSets.main.java
    classpath = configurations.compile
    options.compilerArgs = [
            "-proc:only",
            "-processor",
            "com.querydsl.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = queryDslOutput
}


compileTestJava.dependsOn(generateQueryDSL)

//clean task시 QClass 제거
clean {
    delete file('./src/main/querydsl_generated')
}

dependencies {
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.0.1'
    providedCompile group: 'javax.servlet.jsp', name: 'javax.servlet.jsp-api', version:'2.2.1'
    compile group: 'org.springframework', name: 'spring-webmvc', version:'4.2.0.RELEASE'
    compile group: 'org.springframework', name: 'spring-orm', version:'4.2.0.RELEASE'
    compile group: 'org.springframework', name: 'spring-jdbc', version:'4.2.0.RELEASE'
    compile group: 'org.apache.commons', name: 'commons-dbcp2', version:'2.1.1'
    compile group: 'javax.servlet', name: 'jstl', version:'1.2'
    compile group: 'org.springframework', name: 'spring-tx', version:'4.2.0.RELEASE'
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.6'
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version:'4.3.10.Final'
    compile group: 'com.querydsl', name: 'querydsl-jpa', version:'4.1.4'
    compile group: 'com.querydsl', name: 'querydsl-core', version:'4.1.4'
    compile group: 'com.querydsl', name: 'querydsl-apt', version:'4.1.4'
    compile group: 'com.querydsl', name: 'querydsl-sql', version: '4.1.4'
    compile group: 'org.hibernate', name: 'hibernate-validator', version:'5.2.4.Final'
    runtime group: 'ch.qos.logback', name: 'logback-classic', version:'1.1.1'
    runtime group: 'com.h2database', name: 'h2', version:'1.4.194'
    testCompile group: 'org.springframework', name: 'spring-test', version:'4.2.0.RELEASE'
    testCompile group: 'junit', name: 'junit', version:'4.12'
}

uploadArchives {
    repositories {
        flatDir { dirs 'repos' }
    }
}
